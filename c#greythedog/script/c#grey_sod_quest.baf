/* spawn FF Scout */
IF 
	%IT_IS_SOD%
	RealGlobalTimerExpired("C#Grey_SoDQuestTimer","GLOBAL")
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	GlobalLT("bd_plot","global",395)
	InMyArea("BDBELEGA")
	!Dead("BDBELEGA")
	!See("BDBELEGA")
	Global("C#Grey_SoDQuest","GLOBAL",1)
	InMyArea("ff_camp")
	NextTriggerObject("ff_camp") IsOverMe("C#Grey")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Grey_SoDQuest","GLOBAL",2)
		SetGlobal("C#Gr_SoDQuestThisArea","MYAREA",1)
		CreateCreatureObjectOffset("C#GRSQ02",Myself,[30.30])
		SetInterrupt(TRUE)
END

/* reset timer in bd3000 */
IF 
	%IT_IS_SOD%
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	AreaCheck("bd3000")
	Global("C#Gr_SoDQuestThisArea","MYAREA",0)
	Global("C#Grey_SoDQuest","GLOBAL",4)
	Global("C#Grey_SoDQuestTimeRes","MYAREA",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#Grey_SoDQuestTimer","GLOBAL",800)
		SetGlobal("C#Grey_SoDQuestTimeRes","MYAREA",1)
END



/* bd3000: spawn refugee boy */
IF 
	%IT_IS_SOD%
	RealGlobalTimerExpired("C#Grey_SoDQuestTimer","GLOBAL")
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	GlobalGT("bd_plot","global",309)
	GlobalLT("bd_plot","global",395)
	AreaCheck("bd3000")
	Range("PlayerChest00",30)
	Global("C#Grey_SoDQuest","GLOBAL",4)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Grey_SoDQuest","GLOBAL",5)
		CreateCreatureObjectOffset("C#GRSQ01",Myself,[20.20])
		SetInterrupt(TRUE)
END

/* Grey wants to eat the ham */
/* activation */
IF 
	%IT_IS_SOD%
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	!See("C#GRSQ01")
	PartyHasItem("c#grsq01")
	Global("C#Grey_SoDQuest","GLOBAL",6)
THEN
	RESPONSE #100
		SetGlobal("C#Grey_SoDQuest","GLOBAL",7)
END

/* initiate */
IF 
	%IT_IS_SOD%
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Global("C#Grey_SoDQuest","GLOBAL",7)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* spawn refugee who praises BG's generosity */
IF 
	%IT_IS_SOD%
	RealGlobalTimerExpired("C#Grey_SoDQuestTimer","GLOBAL")
	OR(2) InParty("C#Grey") Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	GlobalLT("bd_plot","global",395)
	AreaCheck("bd3000")
	Global("C#Grey_SoDQuest","GLOBAL",8)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Grey_SoDQuest","GLOBAL",9)
		CreateCreatureObjectOffset("C#GRSQ06",Myself,[20.20])
		SetInterrupt(TRUE)
END


