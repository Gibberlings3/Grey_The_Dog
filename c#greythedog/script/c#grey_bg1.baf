

/* BG1 */

/* BG1, first joining */

IF
	Global("C#Grey_Exists","GLOBAL",2)
	InParty("%IMOEN_DV%")
	Detect(Player1)
	Range(Player1,22)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See([ENEMY])
	CombatCounter(0)
THEN
  RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("C#Grey_Exists","GLOBAL",2)
	InParty("%IMOEN_DV%")
	!Range(Player1,20)
THEN
  RESPONSE #100
	MoveToObject(Player1)
END

IF 
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	GlobalTimerExpired("C#Grey_JondalarTimer","GLOBAL")
	CombatCounter(0) !See([ENEMY])
	See(Player1) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!Dead("Jondal")
	Global("C#Grey_GreyJondalar","GLOBAL",0)
/* //CreateCreatureObjectOffScreen can lead to problems in EE
THEN
	RESPONSE #100
		SetGlobal("C#Grey_GreyJondalar","GLOBAL",1)
		CreateCreatureObjectOffScreen("C#GrJond",Myself,0,0,0) //
		CreateCreatureObjectOffScreen("C#GrErik",Myself,0,0,0) //
END
*/

THEN
	RESPONSE #100
		SetGlobal("C#Grey_GreyJondalar","GLOBAL",1)
		CreateCreatureObjectOffSet("C#GrJond",Myself,[-5.0]) 
		CreateCreatureObjectOffSet("C#GrErik",Myself,[0.-5]) 
END


/* search for Rufie */
IF
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	CombatCounter(0) !See([ENEMY])
	See(Player1) 
	InMyArea("Rufie")
	!Dead("Rufie")
	InMyArea("Albert")
	!Dead("Albert")
	Global("C#Grey_SearchRufieBG1","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		Face(12)
		DisplayStringHead(Myself,@50002)
		Wait(1)
		Face(6)
		DisplayStringHead(Myself,@50003)
		SetGlobal("C#Grey_SearchRufieBG1","GLOBAL",2)
		SetInterrupt(TRUE)
END

/* stop searching in case Rufie is there */
IF
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	CombatCounter(0) !See([ENEMY])
	InMyArea("Rufie")
	!Dead("Rufie")
	InMyArea("Albert")
	!Dead("Albert")
	Global("C#Grey_SearchRufieBG1","GLOBAL",2)
	Range("Rufie",15)
	Global("Rufie","GLOBAL",2)
THEN
	RESPONSE #100
		FaceObject(Player1)
		DisplayStringHead(Myself,@50005)
		FaceObject("Rufie")
		SetGlobal("C#Grey_SearchRufieBG1","GLOBAL",3)
END

/* stop searching in case there is a fight */
IF
	OR(8) 
		Global("C#GreyJoined","GLOBAL",0)
		!CombatCounter(0) 
		See([ENEMY])
		!InMyArea("Rufie")
		Dead("Rufie")
		!InMyArea("Albert")
		Dead("Albert")
		!PartyHasItem("MISC63")
	Global("C#Grey_SearchRufieBG1","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#Grey_SearchRufieBG1","GLOBAL",0)
END

IF
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	CombatCounter(0) !See([ENEMY])
	See(Player1) 
	InMyArea("Rufie")
	!Dead("Rufie")
	InMyArea("Albert")
	!Dead("Albert")
	Global("C#Grey_SearchRufieBG1","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject("Rufie")
END

/* Albert & Rufie gone */
IF
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	CombatCounter(0) !See([ENEMY])
	See(Player1) 
	See("Albert")
	!Dead("Albert")
	GlobalLT("Rufie","GLOBAL",3)
	Global("C#GR_SeenAlbertBG1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#GR_SeenAlbertBG1","GLOBAL",1)
END
IF 
	OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("Rufie","GLOBAL",3)
	!InMyArea("Albert")
	!Dead("Albert")
	Global("C#GR_SeenAlbertBG1","GLOBAL",1)
THEN
	RESPONSE #100
		DisplayStringHead("C#Grey",@50006)
		SetGlobal("C#GR_SeenAlbertBG1","GLOBAL",2)
END



/* know Bhaal: also if BG1NPC is installed (needed for PID )*/

IF %BGT_VAR%
OR(2)
 Global("P#KnowBhaal","GLOBAL",1)
 PartyHasItem("%tutu_var%SCRL2J")
Global("C#Grey_KnowBhaal","GLOBAL",0)
THEN
 RESPONSE #100
 SetGlobal("C#Grey_KnowBhaal","GLOBAL",1)
END

/* remove journal entry: Brage */

IF
	GlobalGT("BrageRiddle","GLOBAL",1)
	Global("C#_GreyBrageRiddle","LOCALS",0)
THEN
  RESPONSE #100
		SetGlobal("C#_GreyBrageRiddle","LOCALS",1)
		EraseJournalEntry(@100000)
END

/* remove journal entry: Brun's son */


IF
	PartyHasItem("%tutu_var%MISC67")
	Global("C#_GreyBrunsSon","LOCALS",0)
THEN
  RESPONSE #100
		SetGlobal("C#_GreyBrunsSon","LOCALS",1)
		EraseJournalEntry(@100002)
END


/* Grey sniffed out a doppelganger */

IF
	Global("C#Grey_SniffedDoppelG","GLOBAL",0)
	OR(4) 
		GlobalGT("C#Grey_BG1Quest","GLOBAL",1) 
		Global("C#Grey_DOPMER_0","GLOBAL",1) 
		Global("C#Grey_DREPPI3_0","GLOBAL",1) 
		Global("C#Grey_SecondBG1Quest","GLOBAL",4)
THEN
	RESPONSE #100
		AddJournalEntry(@100025,INFO)
		SetGlobal("C#Grey_SniffedDoppelG","GLOBAL",1)
END


/* werewolf island - Grey sniffs people */

/* Activate */
IF 
AreaCheck("%IsleofBalduranS%")
OR(3)
	See("Farthing")
	See("Evalt")
	See("Jorin")
Global("C#Grey_WerewolfIsland","GLOBAL",0) 
OR(2) InParty(Myself) Global("C#GreyJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C#Grey_WerewolfIsland","GLOBAL",1)
END

/* Initiate */
IF 
Global("C#Grey_WerewolfIsland","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

/* remove journal entry about Brage's Gauntlets if Brage quest is finished */

IF
CombatCounter(0)
!See([ENEMY])
Global("C#Grey_Brage_Journal","LOCALS",0)
GlobalGT("C#Grey_Brage","GLOBAL",0)
GlobalGT("C#Grey_MetBrage","GLOBAL",0)
THEN
	RESPONSE #100
		EraseJournalEntry(@100001)
		SetGlobal("C#Grey_Brage_Journal","LOCALS",1)
END